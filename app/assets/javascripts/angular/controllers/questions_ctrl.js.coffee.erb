App.controller 'QuestionsCtrl', ['$scope', 'Question', 'Instrument', '$filter', ($scope, Question, Instrument, $filter) ->
  # $scope.questions = []
  $scope.allQuestions = []
  $scope.selectedQuestions = []
  $scope.searchText = ""
  $scope.totalQuestions = 0
  $scope.currentPaginationNumber = 1
  $scope.questionsPerPage = <%= Settings.questions_per_page %>
  $scope.maxNumOfLinks = <%= Settings.maximum_number_of_links %>
  $scope.toolBar = [
      ['h1', 'h2', 'h3', 'h4', 'h5', 'h6', 'p', 'pre', 'quote'],
      ['justifyLeft', 'justifyCenter', 'justifyRight', 'indent', 'outdent'],
      ['bold', 'italics', 'underline', 'strikeThrough', 'ul', 'ol', 'redo', 'undo', 'clear'],
      ['html', 'wordcount', 'charcount']
  ]

  $scope.init = (project_id, instrument_id) ->
    $scope.project_id = project_id
    $scope.instrument_id = instrument_id
    $scope.getQuestionsPerPage(1)
    $scope.allQuestions = Question.query({
      "project_id": project_id,
      "instrument_id": instrument_id
    } )
    $scope.instruments = Instrument.query({"project_id": project_id} , -> $scope.filterInstruments())

  $scope.pagination = current: $scope.currentPaginationNumber

  $scope.getQuestionsPerPage = (pageNumber) ->
    $scope.currentPaginationNumber = pageNumber
    $scope.questions = Question.query({
      "project_id": $scope.project_id,
      "instrument_id": $scope.instrument_id,
      "page": pageNumber
      } , ->
        $scope.questions = $filter('orderBy')($scope.questions, 'number_in_instrument', false)
    )

  $scope.filterInstruments = () ->
    $scope.instrument = $filter('filter')($scope.instruments, id: $scope.instrument_id, true)[0]
    $scope.totalQuestions = $scope.instrument.question_count

  $scope.sortableOptions = {
    cursor: 'move',
    handle: '.move-question',
    axis: 'y',
    stop: (e, ui) -> $scope.updateAndSaveQuestionNumbers()
  }

  $scope.editQuestion = (question) ->
    $scope.$broadcast('EDIT_QUESTION', question.id)
    $scope.edit_question = question
    $scope.edit_question.project_id = $scope.project_id
    $scope.edit_question.instrument_id = $scope.instrument_id

  $scope.newQuestion = (currentQuestion) ->
    question = new Question()
    question.project_id = $scope.project_id
    question.instrument_id = $scope.instrument_id
    question.question_identifier = "q_#{question.project_id}_#{question.instrument_id}_#{$scope.uniqueId()}"
    if currentQuestion == null
        question.number_in_instrument = $scope.questions.length + 1
        $scope.questions.push(question)
    else
        question.number_in_instrument = currentQuestion.number_in_instrument + 1
        $scope.questions.splice($scope.questions.indexOf(currentQuestion) + 1, 0, question)
    $scope.edit_question = question
    indexOfChange = $scope.questions.indexOf(question)
    for qst in $scope.questions
        if $scope.questions.indexOf(qst) > indexOfChange
            qst.number_in_instrument = qst.number_in_instrument + 1

  $scope.saveQuestionSuccess = (data, headers) ->
    $scope.$broadcast('SAVE_QUESTION', data.id)
    $scope.edit_question = null

  $scope.saveQuestionFailure = (result, headers) ->
    angular.forEach result.data.errors, (error, field) ->
      alert error

  $scope.saveQuestion = ->
    if $scope.edit_question.id
      $scope.edit_question.$update({} ,
        (data, headers) -> $scope.saveQuestionSuccess(data, headers),
        (result, headers) -> $scope.saveQuestionFailure(result, headers)
      )
    else
      $scope.edit_question.$save({} ,
        (data, headers) -> $scope.saveQuestionSuccess(data, headers),
        (result, headers) -> $scope.saveQuestionFailure(result, headers)
      )
    $scope.edit_question = null

  $scope.cancelQuestion = ->
    $scope.$broadcast('CANCEL_QUESTION')
    $scope.edit_question = null

  $scope.toggleSelection = (question) ->
    if $scope.questionSelected(question)
      $scope.selectedQuestions.splice($scope.selectedQuestions.indexOf(question), 1)
    else
      $scope.selectedQuestions.push question

  $scope.questionSelected = (question) ->
    question in $scope.selectedQuestions

  $scope.deleteQuestion = (question) ->
    if confirm("Are you sure you want to delete this question?")
      question.project_id = $scope.project_id
      question.$delete({} ,
        (data) ->
          question.id = null
        ,
        (data) ->
          alert "Failed to delete question"
        )
      $scope.questions.splice($scope.questions.indexOf(question), 1)
      $scope.updateQuestionNumbers()

  $scope.uniqueId = ->
    new Date().getTime().toString(36).split("").reverse().join("")

  $scope.questionsBefore = (question) ->
    if $scope.allQuestions.length == 0
        $scope.questions.slice(0, question.number_in_instrument - 1)
    else
        $scope.allQuestions.slice(0, (question.number_in_instrument - 1))

  $scope.questionsAfter = (question) ->
    if question?
      $scope.allQuestions.slice(question.number_in_instrument, $scope.allQuestions.length)

  $scope.questionTypes = ->
    <%= Settings.question_types %>

  $scope.questionTypesWithOptions = (questionType) ->
    questionType in <%= Settings.question_with_options %>

  $scope.questionTypesWithoutOptions = (questionType) ->
    questionType in <%= Settings.question_without_options %>

  $scope.questionTypesWithImages = (questionType) ->
    questionType in <%= Settings.question_with_images %>

  $scope.questionTypesWithMultipleSkipPatterns = (questionType) ->
    questionType in <%= Settings.question_with_multiple_skips %>

  $scope.criticalTypeQuestions = (questionType) ->
    questionType in <%= Settings.critical_type_questions %>

  $scope.repositionQuestion = (question) ->
    question.project_id = $scope.project_id
    question.$update({} ,
      () ->
        $scope.questions = $scope.questions.filter (q) -> q isnt question
        if $scope.questionOnPage(question)
          new_index = question.number_in_instrument - 1 - (($scope.currentPaginationNumber - 1) * $scope.questionsPerPage)
          $scope.questions.splice(new_index, 0, question)
        $scope.updateQuestionNumbers()
    )

  $scope.updateQuestionNumbers = ->
    offset = ($scope.currentPaginationNumber - 1) * $scope.questionsPerPage
    angular.forEach $scope.questions, (question, index) ->
      question.number_in_instrument = offset + index + 1

  $scope.updateAndSaveQuestionNumbers = ->
    offset = ($scope.currentPaginationNumber - 1) * $scope.questionsPerPage
    angular.forEach $scope.questions, (question, index) ->
      question.number_in_instrument = offset + index + 1
      if question.id and $scope.questionOnPage(question)
        question.project_id = $scope.project_id
        question.$update()

  $scope.questionOnPage = (question) ->
    first_number = $scope.questions[0].number_in_instrument
    last_number = $scope.questions[$scope.questions.length - 1].number_in_instrument
    question.number_in_instrument >= first_number and question.number_in_instrument <= last_number

  $scope.searchItem = () ->
    if $scope.searchText is ""
      $scope.getQuestionsPerPage($scope.currentPaginationNumber)
    else
      $scope.questions = $filter('filter')($scope.allQuestions, $scope.searchText)

]